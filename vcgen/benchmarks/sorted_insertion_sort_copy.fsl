(Var x Loc)
(Var k Int)
(Var ret Int)
(Var tmp Loc)
(Var tmp2 Loc)

(Function next Loc Loc)
(Function key Loc Int)
(RecFunction Sorted Loc Bool)
(RecFunction Keys Loc SetInt)

(Var plus_infty Int)
(RecFunction Min Loc Int)
(RecDef (Min x) (ite (= x nil) plus_infty (ite (< (key x) (Min (next x))) (key x) (Min (next x)) ) ))


(RecDef (Sorted x) (ite (= x nil) True (and (Sorted (next x)) (not (IsMember x (Sp (Sorted (antiSp (next x)))))) (<= (key x) (Min (next x))) )))
(RecDef (Keys x) (ite (= x nil) EmptySetInt (SetAdd (Keys (next x)) (key x) ) ))


(RecFunction List Loc Bool)
(RecDef (List x) (ite (= x nil) True (and (List (next x)) (not (IsMember x (Sp (List (antiSp (next x))))))) ))

(Var oldkeysx SetInt)
(Var aux Loc)
(Var intaux Loc)
(Var oldkeysaux SetInt)
(Var oldnxt Loc)
(Var oldkeystmp SetInt)

(lemma (x) (< (key x) plus_infty))
(lemma (x) (=> (Sorted x) (= (SPKeys x) (SPSorted x)) ))
(lemma (x) (=> (Sorted x) (List x)))

(Program sorted_insert (x k oldkeysx ret))
(Pre (Sorted x))
(Post (and
(Sorted ret)
(= (Keys ret) (SetAdd oldkeysx k))
))

(assume (= oldkeysx (Keys x)) )
(If (= x nil)
Then
(alloc tmp)
(assume (not (= tmp nil)))
(assign (key tmp) k)
(assign (next tmp) nil)
(assign ret tmp)
Else
    (If (< (key x) k)
    Then
    (assign aux (next x))
    (assign oldkeysaux (Keys aux))
    (assign oldnxt (next x))
    (call sorted_insert (aux k oldkeysaux tmp))
    (assume (=> (IsSubset (Old (Keys oldnxt)) (Keys tmp)) (<= (Old (Min oldnxt)) (Min tmp))))
    (assign (next x) tmp)
    (assign ret x)
    Else
    (alloc tmp)
    (assume (not (= tmp nil)))
    (assign (key tmp) k)
    (assign (next tmp) x)
    (assign ret tmp)
    )
)
(return)

(Program insertion_sort_copy (x oldkeysx ret))
(Pre (List x))
(Post (and
(List ret)
(Sorted ret)
(= (Keys ret) oldkeysx)
))

(assume  (= oldkeysx (Keys x)))
(If (= x nil)
Then
(assign ret x)
Else
(assign aux (next x))
(assume  (= oldkeysaux (Keys aux)))
(call insertion_sort_copy (aux oldkeysaux tmp))
(assign intaux (key x))
(assign oldkeystmp (Keys tmp))
(call sorted_insert (tmp intaux oldkeystmp tmp2))
(assign ret tmp2)
)
(return)
